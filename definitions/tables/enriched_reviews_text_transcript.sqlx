config { 
  type: "table",
  schema: "enriched",
  name: "reviews_text_transcript",

  dependencies: ["llm_models"],
  tags: ["etl"]
}

WITH reviews AS (
  SELECT *
    ,ROW_NUMBER() OVER (PARTITION BY trip_id ORDER BY updated_at DESC) AS row_num
  FROM (
    SELECT * FROM ${ref("raw","reviews_text")}
    UNION ALL
    --TODO
    SELECT * FROM ${ref("raw","reviews_text_static_sample")}
  ) t
  QUALIFY row_num = 1
)
SELECT * EXCEPT(updated_at, ml_generate_text_result, ml_generate_text_status)
  ,ml_generate_text_status
  ,ml_generate_text_result
  ,JSON_VALUE(ml_generate_text_result, '$.predictions[0].content') AS customer_review_text_transcript
  ,CURRENT_TIMESTAMP() as updated_at
FROM ML.GENERATE_TEXT(
  MODEL `${constants.VARS.PROJECT_ID}.${constants.VARS.LLM_MODEL_BISON}`
  ,(SELECT *
      ,CONCAT('The following phrase is a customer review for a rideshare company. ',
        'Fix grammar and punctuation in the phrase. ',
        'Correct any words that might be out of context (e.g. ride chair should be rideshare). ', 
        'Phrase: ', customer_review_text) as prompt
    FROM reviews
  )
  ,STRUCT(
    1  AS temperature,
    512 AS max_output_tokens,
    0  AS top_p,
    1  AS top_k
  )
)